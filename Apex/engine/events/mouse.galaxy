//=========================================================
//		Apex Roleplaying Engine
//		Copyright (c) 2022, Apex Group
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// LibapxMouseMoved
// ==================
// Updates player's mouse position when mouse moves.
// If player is using mouse-click spawning, updates the spawn splat's
// properties (position, facing, scale, height, etc...) based on player's
// mouse movements.
bool LibapxMouseMoved(bool testConds, bool runActions) {
	// update mouse in world position
	gv_LibapxPlayer[EventPlayer()].mousePosition = Point(EventMouseMovedPosXWorld(), EventMouseMovedPosYWorld());

	// If player mouse-click spawning, perform splat updates:
	// Y plane (mouse up/down) increases or decreases -> adjust splat properties
	// note: Y = 0 is at TOP OF SCREEN, so moving mouse down is a Y increase
	// therefore the scale increment is inverted compared to the Y values
	if( gv_LibapxPlayer[EventPlayer()].isPlacingUnit ) {
		// update vertical movement splat properties (scale, height)
		if( EventMouseMovedPosYUI() > PointGetY(gv_LibapxPlayer[EventPlayer()].mousePositionUi) ) {
			LibapxSpawnSplatUpdateProps(EventPlayer(), false);
		} else { // moving up, so scale up
			LibapxSpawnSplatUpdateProps(EventPlayer(), true);
		}
		// update position and facing angle
		LibapxSpawnSplatUpdatePos();
	}
	// update mouse in UI position:
	gv_LibapxPlayer[EventPlayer()].mousePositionUi = Point(EventMouseMovedPosXUI(), EventMouseMovedPosYUI());
	
	return true;
}

// LibapxMouseLeftCLickedDown
// ========================
// Sets mouse position when player presses left mouse button down.
// If player is using mouse-click spawning, creates a copy of the spawn splat
// at the location in the world that was clicked on
bool LibapxMouseLeftCLickedDown(bool testConds, bool runActions) {
	int user = EventPlayer();
	gv_LibapxPlayer[EventPlayer()].mousePosition = Point(EventMouseClickedPosXWorld(), EventMouseClickedPosYWorld());

	if( gv_LibapxPlayer[EventPlayer()].isPlacingUnit ) {
		LibapxUnitCreate( UnitGetType(gv_LibapxPlayer[EventPlayer()].spawnSplat),
							EventPlayer(),
							UnitGetPosition(gv_LibapxPlayer[EventPlayer()].spawnSplat),
							UnitGetFacing(gv_LibapxPlayer[EventPlayer()].spawnSplat),
							gv_LibapxPlayer[EventPlayer()].spawningCount
						  );
	}

        gv_LibapxPlayer[user].mouseLeft = true;
        LibapxUnitKeypressCheck("key_mouseLeft",gv_LibapxPlayer[user].mouseLeft);
        while(gv_LibapxPlayer[user].mouseLeft == true && KeypressTimerCheck("key_mouseLeft")){
            Wait(KeypressTimer("key_mouseLeft"),c_timeGame);
            if(gv_LibapxPlayer[user].mouseLeft == true){
            LibapxUnitKeypressCheck("key_mouseLeft",gv_LibapxPlayer[user].mouseLeft);
            }
        }

	return true;
}
bool LibapxMouseLeftCLickedUp(bool testConds, bool runActions) {
	int user = EventPlayer();
	gv_LibapxPlayer[user].mouseLeft = false;

	LibapxUnitKeypressUpCheck("keyUp_mouseLeft");

	return true;
}

// librtprMouseRightClicked_
// =======================
// toggles player right mouse button down/up flag
bool LibapxMouseRightClickedDown(bool testConds, bool runActions) {
	int user = EventPlayer();

	gv_LibapxPlayer[EventPlayer()].mouseRight = true;

	LibapxUnitKeypressCheck("key_mouseRight",gv_LibapxPlayer[user].mouseRight);
	while(gv_LibapxPlayer[user].mouseRight == true && KeypressTimerCheck("key_mouseRight")){
		Wait(KeypressTimer("key_mouseRight"),c_timeGame);
		if(gv_LibapxPlayer[user].mouseRight == true){
		LibapxUnitKeypressCheck("key_mouseRight",gv_LibapxPlayer[user].mouseRight);
		}
	}

	return true;
}
bool LibapxMouseRightClickedUp(bool testConds, bool runActions) {
	int user = EventPlayer();

	LibapxUnitKeypressUpCheck("keyUp_mouseRight");

	gv_LibapxPlayer[EventPlayer()].mouseRight = false;
	return true;
}


// librtprMouseMiddleClicked_
// =======================
// toggles player middle mouse button down/up flag
bool LibapxMouseMiddleClickedDown(bool testConds, bool runActions) {
	int user = EventPlayer();

	gv_LibapxPlayer[EventPlayer()].mouseMiddle = true;

	LibapxUnitKeypressCheck("key_mouseMiddle",gv_LibapxPlayer[user].mouseMiddle);
	while(gv_LibapxPlayer[user].mouseMiddle == true && KeypressTimerCheck("key_mouseMiddle")){
		Wait(KeypressTimer("key_mouseMiddle"),c_timeGame);
		if(gv_LibapxPlayer[user].mouseMiddle == true){
		LibapxUnitKeypressCheck("key_mouseMiddle",gv_LibapxPlayer[user].mouseMiddle);
		}
	}

	return true;
}

bool LibapxMouseMiddleClickedUp(bool testConds, bool runActions) {
	int user = EventPlayer();

	LibapxUnitKeypressUpCheck("keyUp_mouseMiddle");

	gv_LibapxPlayer[EventPlayer()].mouseMiddle = false;
	return true;
}
